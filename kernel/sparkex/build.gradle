version 'unspecified'

apply plugin: 'java'
apply plugin: 'scala'

sourceCompatibility = 1.8

ext {
  kernelIdName = 'scala'
  scalaVersion = '2.12'
  scalaVersionFull = "2.12.13"
}

scala {
  zincVersion = "1.3.5"
}

configurations {
    provided
    antlr
}


sourceSets {
    main {
        compileClasspath += configurations.provided
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

repositories {
    mavenCentral()
}
dependencies {
    provided project(':base')
    provided project(':scala')

    provided group: 'org.apache.spark', name: "spark-core_${scalaVersion}", version: '2.4.7'
    provided group: 'org.apache.spark', name: "spark-sql_${scalaVersion}", version: '2.4.7'
    provided('com.twosigma:flint:0.6.0'){
        transitive = false
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile project(':base').sourceSets.test.output
    testCompile project(':scala').sourceSets.test.output
}

configurations.all {
  resolutionStrategy {
    force "org.scala-lang:scala-library:${scalaVersionFull}"
    force "org.scala-lang:scala-compiler:${scalaVersionFull}"
  }
}

task copyDependencies(type: Copy) {
    from configurations.runtime
    from jar
    into new File(pathToStaticContent, 'sparkex/lib')
}

jar.finalizedBy copyDependencies

staticContent {
    ext.executeStaticContent = false
}
