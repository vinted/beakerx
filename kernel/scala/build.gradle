/*
 *  Copyright 2017 TWO SIGMA OPEN SOURCE, LLC
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *         http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven-publish'

ext {
  kernelIdName = 'scala'
  scalaVersion = '2.12'
  scalaVersionFull = "2.12.13"
}

scala {
  zincVersion = "1.3.5"
}

mainClassName = 'com.twosigma.beakerx.scala.kernel.Scala'

configurations {
  provided
  antlr
}

//need for compiling scala before java
sourceSets {
  main {
    compileClasspath += configurations.provided
    scala {
      srcDirs = ['src/main/scala', 'src/main/java']
    }
    java {
      srcDirs = []
    }
  }
  test {
    compileClasspath += configurations.provided
    runtimeClasspath += configurations.provided
  }
}

repositories {
  mavenCentral()
}

dependencies {
  provided project(':base')
  compile group: 'org.scala-lang', name: 'scala-compiler', version: scalaVersionFull
  compile group: 'org.scala-lang', name: 'scala-library', version: scalaVersionFull
  compile group: 'org.scala-lang.modules', name: "scala-parser-combinators_${scalaVersion}", version: '1.0.7'
  compile group: 'jline', name: 'jline', version: '2.12'

  testCompile group: 'org.scalatest', name: "scalatest_${scalaVersion}", version: '3.0.4'
  testCompile project(':base').sourceSets.test.output
}

configurations.all {
  resolutionStrategy {
    force "org.scala-lang:scala-library:${scalaVersionFull}"
    force "org.scala-lang:scala-compiler:${scalaVersionFull}"
  }
}

compileScala {
  scalaCompileOptions.additionalParameters = ['-target:jvm-1.8']
}

jar {
  manifest {
    attributes "Main-Class": "$mainClassName"
  }
}

test {
  maxHeapSize = '1800m'
}

task realclean
realclean.dependsOn(clean)
// should also remove
// GroovyLexer.java GroovyLexer.tokens GroovyParser.java GroovyParser.tokens
// GroovyParserBaseListener.java GroovyParserListener.java

publishing {
  publications {
    maven(MavenPublication) {
      groupId 'com.twosigma'
      artifactId 'beaker-kernel-scala'
      version '2.0-SNAPSHOT'

      from components.java
    }
  }
}

installDist.into new File(pathToStaticContent, kernelIdName)
distZip.dependsOn installDist
